configfile: "./snakefile_config"

QSEQIDS = []
with open(config['query_sequence'],'r') as qseqfile:
	for line in qseqfile.readlines():
		if ">" in line:
			#TODO adjust qseq_ids in query_sequence file in order to assign a new unique name ? - until now just refseq OR local database sequence headers are allowed
			qseq_id = line.split(" ")[0].split(">")[1].split(".")[0]
			QSEQIDS.append(qseq_id)

rule all:
	input:
		"blast_results.table",
		"blast_results.html",
		"bokeh_plot.html",
		"query_sequence_information.html",
		"query_sequence_information.csv",
		expand("{qseqid}/target_sequences.svg",qseqid=QSEQIDS)

#RULE 1
rule blast:
	input: queries=config['query_sequence']
	output: results="blast_results.table"
	params: word_size=config['word_size'], e_value=config['e_value'], num_alignments=config['num_alignments'], num_threads=config['num_threads'], database=config['blastdb'], entrez_query=config['entrez_query'], search_strategy=config['search_strategy']
	run:
		if(params.entrez_query != ""):
			entrez_query = params.entrez_query
			if "orgn" not in entrez_query:
				entrez_query = entrez_query + "[ORGN]"
			cmd_string = params.search_strategy + " -db "+params.database+\
			" -outfmt \"6 qseqid sseqid pident evalue bitscore slen qgi sgi sacc staxids sscinames scomnames stitle\""\
			" -out "+output.results+\
			" -word_size "+str(params.word_size)+\
			" -evalue "+str(params.e_value)+\
			" -num_alignments "+str(params.num_alignments)+\
			" -query "+input.queries+\
			" -remote"+\
			" -entrez_query \""+entrez_query+"\""
			shell(cmd_string)
		else:
			cmd_string = params.search_strategy + " -db "+params.database+\
			" -outfmt \"6 qseqid sseqid pident evalue bitscore slen qgi sgi sacc staxids sscinames scomnames stitle\""\
			" -out "+output.results+\
			" -word_size "+str(params.word_size)+\
			" -evalue "+str(params.e_value)+\
			" -num_alignments "+str(params.num_alignments)+\
			" -query "+input.queries+\
			" -remote"
			shell(cmd_string)

#RULE 2
rule blast_results_to_plots_and_html_table:
	input: blast_results="blast_results.table", query_file=config['query_sequence']
	params: user_email=config['user_email']
	output: bokeh_plot="bokeh_plot.html", html_table="blast_results.html", taxonomic_table='blast_results_with_tax.table'
	log:log="log/blast_results_to_plots_and_html_table.log"
	script:
		"../../../../static/snakefiles/one_way_blast/blast_results_to_plots_and_html_table.py"

rule query_sequences_to_html_table:
	input: target_file=config['query_sequence']
	params: email=config['user_email']
	output: output_html="query_sequence_information.html", output_csv="query_sequence_information.csv"
	script:
		"../../../../static/snakefiles/one_way_blast/query_sequences_to_html_table.py"

rule filter_blast_results_table_to_csv:
    input: results_table="blast_results.table"
    output: result_csv=expand("{qseqid}/blast_results.table",qseqid=QSEQIDS),
            ids=expand("{qseqid}/target_sequence_ids.txt",qseqid=QSEQIDS)
    log:log="log/filter_blast_results_table_to_csv.log"
    script:
    	"../../../../static/snakefiles/one_way_blast/remote_searches/filter_blast_results_table_to_csv.py"

rule extract_subject_sequences_from_NCBI_edirect:
	input: ids="{qseqid}/target_sequence_ids.txt"
	params: user_email=config['user_email']
	output: fasta_file="{qseqid}/target_sequences.faa"
	log: log="log/{qseqid}/extract_subject_sequences_from_NCBI_edirect.log"
	script:
	    "../../../../static/snakefiles/one_way_blast/remote_searches/extract_subject_sequences_from_NCBI_edirect.py"

rule conduct_multiple_sequence_alignment_mafft:
    input: "{qseqid}/target_sequences.faa"
    output: "{qseqid}/target_sequences.msa"
    log: log="log/{qseqid}/conduct_multiple_sequence_alignment_mafft.log"
    run:
        with open(input[0],'r') as infile:
            if len(infile.readlines()) > 1:
                shell("mafft --auto {input} > {output} 2> {log}")
            else:
                shell("touch {output} 2> {log}")

rule conduct_phylogeny_fast_tree:
    input: "{qseqid}/target_sequences.msa"
    output: "{qseqid}/target_sequences.tree"
    log: log="log/{qseqid}/conduct_phylogeny_fast_tree.log"
    run:
        with open(input[0],"r") as infile:
            if len(infile.readlines()) > 1:
                shell("fasttree {input} > {output} 2> {log}")
            else:
                shell("touch {output} 2> {log}")


rule update_tree_with_taxid:
    input: nw="{qseqid}/target_sequences.tree",
            df= "{qseqid}/blast_results.table"
    output: "{qseqid}/transformed_treefile.nwk"
    log: log= "log/{qseqid}/update_tree_with_taxid.log"
    script:
        "../../../../static/snakefiles/one_way_blast/update_seq_id_taxinfo.py"


rule ete3_tree_to_png:
    input: tree="{qseqid}/target_sequences.tree"
    output: pic="{qseqid}/target_sequences.svg"
    log: log= "log/{qseqid}/ete3_tree_to_png.log"
    script:
        "../../../../static/snakefiles/one_way_blast/remote_searches/ete3_tree_to_png.py"
